#feature SPECULAR_WORKFLOW
#feature WITH_EDGE
#feature CLIP_PLANE
#feature TRIANGLE_INFO


#shader vertex
#version 450 core

#include ":Shaders/Common/Buffers/EngineUBO.ovfxh"
#include ":Shaders/Common/Utils.ovfxh"

layout (location = 0) in vec3 geo_Pos;
layout (location = 1) in vec2 geo_TexCoords;
layout (location = 2) in vec3 geo_Normal;
layout (location = 3) in vec3 geo_Tangent;
layout (location = 4) in vec3 geo_Bitangent;

out VS_OUT
{
    vec3 FragPos;
    vec2 TexCoords;
    vec3 Normal;
    mat3 TBN;

} vs_out;

void main()
{
    vs_out.FragPos = vec3(ubo_Model * vec4(geo_Pos, 1.0));
    vs_out.TexCoords = geo_TexCoords;
    vs_out.Normal = normalize(mat3(transpose(inverse(ubo_Model))) * geo_Normal);
    vs_out.TBN = ConstructTBN(ubo_Model, geo_Normal, geo_Tangent, cross(geo_Normal, geo_Tangent));



    gl_Position = ubo_Projection * ubo_View * vec4(vs_out.FragPos, 1.0);
}

#shader geomerty
#version 450 core

#include ":Shaders/Common/Buffers/EngineUBO.ovfxh"

in VS_OUT
{
    vec3 FragPos;
    vec2 TexCoords;
    vec3 Normal;
    mat3 TBN;

}gs_in[];
out GS_OUT{
    vec3 FragPos;
    vec2 TexCoords;
    vec3 Normal;
    mat3 TBN;
#if defined(WITH_EDGE)
    noperspective vec4 edgeEqn[3];
#endif
} gs_out;

layout(triangles) in;
layout(triangle_strip, max_vertices = 3) out;

//uniform samplerBuffer edgeTexture;

void main() 
{

#if defined(WITH_EDGE)
    vec2 pos[4];
    for (int i = 0; i < 3; i++)
    {
        pos[i] = gl_in[i].gl_Position.xy/gl_in[i].gl_Position.w;
        pos[i] = pos[i]*vec2(0.5) + vec2(0.5);
        pos[i] = pos[i]*vec2(ubo_ScreenWidth,ubo_ScreenHeight);
    }
    pos[3] = pos[0];  

    float ccw = sign(cross(vec3(pos[1] - pos[0], 0.0), vec3(pos[2] - pos[0], 0.0)).z);

    for (int i = 0; i < 3; i++)
    {
        vec2 tmp = normalize(pos[i+1] - pos[i]);
        tmp = ccw*vec2(-tmp.y, tmp.x);
        float d = dot(pos[i], tmp);
        gs_out.edgeEqn[i] = vec4(tmp.x, tmp.y, 0.0, -d);
    }
    
    //float edgeValues = 255.0*texelFetch(edgeTexture, gl_PrimitiveIDIn + PrimitiveIDOffset).r;
    //if (edgeValues < 4.0) edgeEqn[2].z = lineWidth;
    //if (mod(edgeValues, 4.0) < 2.0) edgeEqn[1].z = lineWidth;
    //if (mod(edgeValues, 2.0) < 1.0) edgeEqn[0].z = lineWidth;
#endif
 

    vec3 nor=normalize(cross(gs_in[1].FragPos-gs_in[0].FragPos,gs_in[2].FragPos-gs_in[0].FragPos));      

    for (int i = 0; i < 3; i++)
    {
        gl_PrimitiveID = gl_PrimitiveIDIn;
        gs_out.FragPos=gs_in[i].FragPos;
        gs_out.TexCoords=gs_in[i].TexCoords;
        gs_out.Normal=nor;
        gs_out.TBN=gs_in[i].TBN;
        gl_Position = gl_in[i].gl_Position;
        EmitVertex();
    }
    EndPrimitive();
}


#shader fragment
#version 450 core



#include ":Shaders/Common/Buffers/EngineUBO.ovfxh"
#include ":Shaders/Lighting/PBR.ovfxh"


in GS_OUT
{
    vec3 FragPos;
    vec2 TexCoords;
    vec3 Normal;
    mat3 TBN;
#if defined(WITH_EDGE)
    noperspective vec4 edgeEqn[3];
#endif
} fs_in;

// surface with edges
//uniform float lineWidth;
//uniform vec3 edgeColor;
//uniform float edgeOpacity;



uniform sampler2D u_AlbedoMap;
uniform vec4 u_Albedo = vec4(1.0);

uniform sampler2D u_AmbientOcclusionMap;
uniform sampler2D u_MaskMap;

uniform sampler2D u_EmissiveMap;
uniform vec3 u_EmissiveColor = vec3(1.0, 1.0, 1.0);
uniform float u_EmissiveIntensity = 0.0;

#if defined(SPECULAR_WORKFLOW)
uniform sampler2D u_SpecularMap;
uniform float u_Specular = 1.0;
uniform sampler2D u_GlossinessMap;
uniform float u_Glossiness = 1.0;
#else
uniform sampler2D u_MetallicMap;
uniform float u_Metallic = 1.0;
uniform sampler2D u_RoughnessMap;
uniform sampler2D u_RoughnessMetallicMap;
uniform float u_Roughness = 1.0;
#endif

uniform float u_RefractionIndex = 1.5; // 1.0 = air, 1.33 = water, 1.5 = glass, etc.
uniform float u_Transmission = 0.0; // 0.0 = opaque, 1.0 = fully transmissive
uniform vec2 u_TextureTiling = vec2(1.0, 1.0);
uniform vec2 u_TextureOffset = vec2(0.0, 0.0);

uniform float u_AlphaClippingThreshold = 0.0;
uniform bool u_AlphaDithering = false;

// Useful when no post-processing is applied.
uniform bool u_BuiltInToneMapping = false;
uniform bool u_BuiltInGammaCorrection = false;

#if defined(TRIANGLE_INFO)
uniform usamplerBuffer triangleInfoTex;
uniform int trifoLen=0;
#endif

uniform sampler2D _ShadowMap;
uniform mat4 _LightSpaceMatrix;
uniform samplerCube _EnvironmentMap;

uniform samplerBuffer domainColorTex;

out vec4 FRAGMENT_COLOR;

void main()
{
#if defined(CLIP_PLANE)
    float offsetw=dot(ubo_plane.xyz,fs_in.FragPos)+ubo_plane.w;
    if(offsetw>0){
         discard;
    }
#endif


    vec2 texCoords = TileAndOffsetTexCoords(fs_in.TexCoords, u_TextureTiling, u_TextureOffset);
    vec4 albedo = texture(u_AlbedoMap, texCoords) * u_Albedo;
    albedo.a *= texture(u_MaskMap, texCoords).r;
    if (albedo.a < u_AlphaClippingThreshold)
    {
        discard;
    }
    
    int domainIndex=int(round(fs_in.TexCoords.x));
    albedo.rgb=texelFetch(domainColorTex,domainIndex).rgb;

#if defined(TRIANGLE_INFO)
    for(int i=0;i<trifoLen;i++){
        uint tid=texelFetch(triangleInfoTex,i).g;
        if(gl_PrimitiveID==tid){
            albedo.rgb=vec3(1,1,1);
            break;
        }
    }
#endif
   


    if (u_AlphaDithering && Dithering(albedo.a, gl_FragCoord.xy) < 0)
    {
        discard;
    }


    vec3 normal = normalize(fs_in.Normal);

    vec3 VD=normalize(ubo_ViewPos-fs_in.FragPos);
    if(ubo_CameraType==0){
       VD=vec3(ubo_View[0][2],ubo_View[1][2],ubo_View[2][2]);
    }
    if(dot(VD,normal)<0.0f){
        normal=-normal;
    }

#if defined(SPECULAR_WORKFLOW)
    const float specular = texture(u_SpecularMap, texCoords).r * u_Specular;
    const float glossiness = texture(u_GlossinessMap, texCoords).r * u_Glossiness;
    const float metallic = 1.0 - specular;
    const float roughness = 1.0 - glossiness;
#else
    const float metallic = texture(u_MetallicMap, texCoords).r * u_Metallic;
    const float roughness = texture(u_RoughnessMap, texCoords).g * u_Roughness;
#endif

    const float ao = texture(u_AmbientOcclusionMap, texCoords).r;

#if defined(WITH_EDGE)
    float edist[3];
    edist[0] = dot(fs_in.edgeEqn[0].xy, gl_FragCoord.xy) + fs_in.edgeEqn[0].w;
    edist[1] = dot(fs_in.edgeEqn[1].xy, gl_FragCoord.xy) + fs_in.edgeEqn[1].w;
    edist[2] = dot(fs_in.edgeEqn[2].xy, gl_FragCoord.xy) + fs_in.edgeEqn[2].w;
    if (edist[0] < -0.5 && fs_in.edgeEqn[0].z > 0.0) discard;
    if (edist[1] < -0.5 && fs_in.edgeEqn[1].z > 0.0) discard;
    if (edist[2] < -0.5 && fs_in.edgeEqn[2].z > 0.0) discard;
    edist[0] += fs_in.edgeEqn[0].z;
    edist[1] += fs_in.edgeEqn[1].z;
    edist[2] += fs_in.edgeEqn[2].z;
    float emix = clamp(0.5 + 0.5*1.0 - min( min( edist[0], edist[1]), edist[2]), 0.0, 1.0);
    albedo.rgb = mix(albedo.rgb, vec3(0.0), emix);
#endif
   vec3 pbr = PBRLightingModel(
        albedo.rgb,
        metallic,
        roughness,
        ao,
        normal,
        VD,
        fs_in.FragPos,
        _ShadowMap,
        _LightSpaceMatrix,
        _EnvironmentMap,
        u_Transmission,
        u_RefractionIndex
    );

    // Simple built-in tonemapping (Reinhard) and gamma correction for elements
    // not affected by post-processing (e.g. debug, UI, etc.), but still aiming
    // to approximate the final PBR look.
    pbr = mix(pbr, pbr / (pbr + vec3(1.0)), u_BuiltInToneMapping);
    pbr = mix(pbr, pow(pbr, vec3(1.0 / 2.2)), u_BuiltInGammaCorrection);

    const vec3 emissive = texture(u_EmissiveMap, texCoords).rgb * u_EmissiveColor * u_EmissiveIntensity;
    pbr += emissive;

    FRAGMENT_COLOR = vec4(pbr, albedo.a);

}